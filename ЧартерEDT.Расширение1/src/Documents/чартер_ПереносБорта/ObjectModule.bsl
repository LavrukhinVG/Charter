#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий   

Процедура ОбработкаПроведения(Отказ, РежимПроведения)

	Если Не чартеры_ОбщиеПроцедурыИФункцииСервер.ПроверитьДубльРегистрацииНаРейс(КонечныйРейс
																, ТабельныйНомер) Тогда 
		
		Движения.чартер_ПереносыРейсов.Записывать = Истина;	
		Запись = Движения.чартер_ПереносыРейсов.Добавить();
		
		Запись.Период				= ТекущаяДатаСеанса();
		Запись.Регистратор 			= Ссылка;	
		Запись.ДокументРегистрации  = ДокументРегистрации;
		Запись.Сотрудник  			= Сотрудник;	
		Запись.ИсходныйРейс     	= ИсходныйРейс;
		Запись.КонечныйРейс     	= КонечныйРейс;
		Запись.ПричинаПереноса		= ПричинаПереноса;	
		Запись.Исполнитель 			= SessionParameters.CurrentUser;
		Запись.ДатаИзменения 		= ТекущаяДатаСеанса();
		
		ПровестиДокументРегистрации(Отказ); 
	Иначе
		Отказ = Истина;
		ИнфоСообщение = СтрШаблон("Сотрудник с табельным номером %1 уже зарегистрирован на переносимый борт %2"
									, ТабельныйНомер, КонечныйРейс);
		чартеры_ОбщиеПроцедурыИФункцииСервер.СообщитьПользователю(ИнфоСообщение);				
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли; 
	
    Если Не ПроверитьЗаполнение() Тогда      
        Возврат;  
    КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПровестиДокументРегистрации(Отказ)

	ДокументОбъект = ДокументРегистрации.ПолучитьОбъект();
	
	ДокументОбъект.Рейс = КонечныйРейс;
	ДокументОбъект.Комментарий = ДокументОбъект.Комментарий + СтрШаблон(". Перенесен с Борт № %1"
																		, ИсходныйРейс.Наименование);
	ДокументОбъект.Исполнитель = SessionParameters.CurrentUser;
	ДокументОбъект.ДатаИзменения = ТекущаяДатаСеанса();

	Попытка
		ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
		ИнфоСообщение = СтрШаблон("Исходный рейс %1 Заменен на %2"
								, ИсходныйРейс.Наименование, КонечныйРейс.Наименование);				
		чартеры_ОбщиеПроцедурыИФункцииСервер.СообщитьПользователю(ИнфоСообщение);						
	Исключение
		Отказ = Истина;
		чартеры_ОбщиеПроцедурыИФункцииСервер.СообщитьПользователю("Не удалось изменить регистрацию сотрудника!");	    
	КонецПопытки; 
	
КонецПроцедуры 

#КонецОбласти  

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли
