#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры = Неопределено Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если Не Параметры.Свойство("ВладелецФайлов") Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("РежимВыбора") И Параметры.РежимВыбора = Истина Тогда
		Элементы.Файлы.РежимВыбора = Истина;
		ЗакрыватьПриВыборе = Истина;
	КонецЕсли;
	
	ВладелецФайлов = Параметры.ВладелецФайлов;
	Заголовок = Строка(ВладелецФайлов);
	
	УстановитьОтборФайлов();
	
	//ЗаполнитьСписокРасширенийФайлов();
	
	ТекущийПользователь = SessionParameters.CurrentUser;
	
	Если Параметры.Свойство("Отбор") Тогда
		
		Для каждого СтрокаОтбора Из Параметры.Отбор Цикл
			
			ПолеОтбора = Новый ПолеКомпоновкиДанных(СтрокаОтбора.Ключ);
			ЭлементОтбора = ПолучитьЭлементОтбора(ПолеОтбора, Файлы.Отбор);
			
			Если ЭлементОтбора = Неопределено Тогда		
				ЭлементОтбора = Файлы.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных")); 
				ЭлементОтбора.ЛевоеЗначение = ПолеОтбора;
			КонецЕсли;
			
			ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
			ЭлементОтбора.ПравоеЗначение = СтрокаОтбора.Значение;
			ЭлементОтбора.Использование = Истина;
			
		КонецЦикла;
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	#Если ВебКлиент Тогда
		Элементы.ФайлыОткрытьФайл.Видимость = Ложь;
	#КонецЕсли
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыФайлы

&НаКлиенте
Процедура ФайлыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Не Элементы.Файлы.РежимВыбора Тогда
		СтандартнаяОбработка = Ложь;
		СохранитьФайл(Элементы.Файлы.ТекущаяСтрока);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ФайлыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
	Если Копирование Тогда
		Возврат;
	КонецЕсли;
	
	СписокВыбора = Новый СписокЗначений;
	СписокВыбора.Добавить("ДобавитьФайлОтчетСотрудник", "Отчет (утвержден сотрудником)");
	СписокВыбора.Добавить("ДобавитьФайлОтчетРуководитель", "Отчет (утвержден руководителем)");
	СписокВыбора.Добавить("ДобавитьФайлПрочий", "Прочий файл");
	
	СписокВыбора.ПоказатьВыборЭлемента(Новый ОписаниеОповещения("ФайлыПередНачаломДобавленияВыборПользователя"
																			, ЭтотОбъект), "Выберите вид нового файла.");
	
КонецПроцедуры

&НаКлиенте
Процедура ФайлыВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Значение) Тогда
		Закрыть(Значение);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаДобавитьФайл(Команда)
	
	ДобавитьФайл(Команда.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОткрытьФайл(Команда)
	
	ОткрытьФайл(Элементы.Файлы.ТекущаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаСохранитьФайл(Команда)
	
	СохранитьФайл(Элементы.Файлы.ТекущаяСтрока);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ДобавитьФайл(ИмяКоманды)
	
	ОписаниеЗавершения = Новый ОписаниеОповещения("ДобавитьФайлыЗавершение", ЭтотОбъект
																			, Новый Структура("ИмяКоманды", ИмяКоманды));
	ОписаниеПроцессаВыполнения = новый ОписаниеОповещения("ДобавитьФайлыПроцессВыполнения", ЭтотОбъект
										, Новый Структура("МаксимальныйРазмерФайла", ПолучитьМаксимальныйРазмерФайла()));
	
	НачатьПомещениеФайловНаСервер(ОписаниеЗавершения, ОписаниеПроцессаВыполнения);
	
КонецПроцедуры
	
&НаСервере
Процедура УстановитьОтборФайлов()
	
	ПолеОтбора = Новый ПолеКомпоновкиДанных("ВладелецФайла");
	ЭлементОтбора = ПолучитьЭлементОтбора(ПолеОтбора, Файлы.Отбор);
	
	Если ЭлементОтбора = Неопределено Тогда		
		ЭлементОтбора = Файлы.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных")); 
		ЭлементОтбора.ЛевоеЗначение = ПолеОтбора;
	КонецЕсли;
	
	ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбора.ПравоеЗначение = ВладелецФайлов;
	ЭлементОтбора.Использование = Истина;

КонецПроцедуры

&НаСервере
Функция ПолучитьЭлементОтбора(ПолеОтбора, ОтборТаблицы)
	
	ТипЭлемент = Тип("ЭлементОтбораКомпоновкиДанных");
	
	Для каждого итЭлемент Из ОтборТаблицы.Элементы Цикл 
		Если ТипЗнч(итЭлемент) = ТипЭлемент И итЭлемент.ЛевоеЗначение = ПолеОтбора Тогда
			Возврат итЭлемент;	
		КонецЕсли;
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьМаксимальныйРазмерФайла()
	//Возврат Константы.job_МаксимальныйРазмерФайла.Получить();
КонецФункции

&НаКлиенте
Процедура ДобавитьФайлыЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат = Неопределено Тогда
		
		Для Каждого ЭлементФайл Из Результат Цикл
			
			Если НЕ ЭлементФайл.ПомещениеФайлаОтменено Тогда
				
				СтруктураДанныхФайла = чартер_ОбщиеПроцедурыИфункцииКлиент.РазложитьПолноеИмяФайла(ЭлементФайл.СсылкаНаФайл.Имя);
				СтруктураДанныхФайла.Вставить("ИмяКоманды", ДополнительныеПараметры.ИмяКоманды);
				СтруктураДанныхФайла.Вставить("РазмерФайла", ЭлементФайл.СсылкаНаФайл.Размер());
				
				Для каждого СтрокаОтбора Из Файлы.Отбор.Элементы Цикл
					СтруктураДанныхФайла.Вставить(СокрЛП(СтрокаОтбора.ЛевоеЗначение), СтрокаОтбора.ПравоеЗначение);
				КонецЦикла;	
				
				СтруктураДанныхФайла.Вставить("РасширениеФайла", ЭлементФайл.СсылкаНаФайл.Расширение);
				СтруктураДанныхФайла.Вставить("ИмяФайла", ЭлементФайл.СсылкаНаФайл.Файл.ИмяБезРасширения);
				СтруктураДанныхФайла.Вставить("Наименование", ЭлементФайл.СсылкаНаФайл.Имя);
				СтруктураДанныхФайла.Вставить("ПолноеИмяФайла", ЭлементФайл.СсылкаНаФайл.Файл.ПолноеИмя);
				
				чартеры_ОбщиеПроцедурыИФункцииСервер.ЗаписатьФайлВСправочникФайлы(ЭлементФайл.Адрес, СтруктураДанныхФайла
																					, ТекущийПользователь, ВладелецФайлов); 
				Элементы.Файлы.Обновить();
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьФайлыПроцессВыполнения(ПомещаемыйФайл, Помещено, ОтказОтПомещенияФайла, ПомещеноВсего
														, ОтказОтПомещенияВсехФайлов, ДополнительныеПараметры) Экспорт
	
	//Если ПомещаемыйФайл.Размер() > ДополнительныеПараметры.МаксимальныйРазмерФайла Тогда   
	//	ОтказОтПомещенияФайла = истина;
	//	ПоказатьПредупреждение(, "Размер файла """ + ПомещаемыйФайл.Имя  + """превышает максимальный размер файла (" + Строка(Окр(ДополнительныеПараметры.МаксимальныйРазмерФайла/1048576, 2)) + " МБ).");
	//КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФайл(ФайлСсылка)
	
	Если ЗначениеЗаполнено(ФайлСсылка) Тогда
		НачатьПолучениеКаталогаВременныхФайлов(Новый ОписаниеОповещения("ОткрытьФайлПродолжение", ЭтотОбъект
																			, Новый Структура("ФайлСсылка", ФайлСсылка)));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФайлПродолжение(ИмяКаталогаВременныхФайлов, ДополнительныеПараметры) Экспорт
	
	ФайлСтруктура = ПолучитьСтруктуруДанныхФайлаНаСервере(ДополнительныеПараметры.ФайлСсылка);
	
	ПолноеИмяВременногоФайла = ИмяКаталогаВременныхФайлов + ФайлСтруктура.ИмяФайла;
	ДанныеФайла = ПолучитьИзВременногоХранилища(ФайлСтруктура.Адрес);
	
	ДанныеФайла.НачатьЗапись(Новый ОписаниеОповещения("ОткрытьФайлЗавершение", ЭтотОбъект, Новый Структура("ПолноеИмяВременногоФайла", ПолноеИмяВременногоФайла)), ПолноеИмяВременногоФайла);

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФайлЗавершение(ДополнительныеПараметры) Экспорт
	
	Если Не ДополнительныеПараметры = Неопределено Тогда
		НачатьЗапускПриложения(Новый ОписаниеОповещения("ОткрытьФайлОкончание", ЭтотОбъект), ДополнительныеПараметры.ПолноеИмяВременногоФайла);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФайлОкончание(КодВозврата, ДополнительныеПараметры) Экспорт
	
	//Код возврата не задан, ничего не делаем...
	Возврат;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьФайл(ФайлСсылка)
	
	Если ЗначениеЗаполнено(ФайлСсылка) Тогда
		
		ФайлСтруктура = ПолучитьСтруктуруДанныхФайлаНаСервере(ФайлСсылка);
		
		#Если ВебКлиент Тогда
			ПараметрыДиалогаПолученияФайлов = Новый ПараметрыДиалогаПолученияФайлов;
			ПараметрыДиалогаПолученияФайлов.ВыборКаталога = Ложь;
			
			НачатьПолучениеФайлаССервера(ФайлСтруктура.Адрес, ФайлСтруктура.ИмяФайла, ПараметрыДиалогаПолученияФайлов);
		#Иначе
			ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
			ДиалогВыбораФайла.Заголовок = "Укажите имя и путь сохраняемого файла";
			ДиалогВыбораФайла.МножественныйВыбор = Ложь;
			ДиалогВыбораФайла.ПолноеИмяФайла = ФайлСтруктура.ИмяФайла;
			
			ДиалогВыбораФайла.Показать(Новый ОписаниеОповещения("СохранитьФайлЗавершение", ЭтотОбъект, ФайлСтруктура));
		#КонецЕсли
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьФайлЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если Не ВыбранныеФайлы = Неопределено Тогда
		НачатьПолучениеФайлаССервера(, ДополнительныеПараметры.Адрес, ВыбранныеФайлы[0]);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьСтруктуруДанныхФайлаНаСервере(ФайлСсылка)
	
	Возврат Справочники.чартер_Файлы.ПолучитьСтруктуруДанныхФайла(ФайлСсылка);
	
КонецФункции

&НаКлиенте
Процедура ФайлыПередНачаломДобавленияВыборПользователя(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
	
	Если Не ВыбранныйЭлемент = Неопределено Тогда
		ДобавитьФайл(ВыбранныйЭлемент.Значение);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти