#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Получить всех зарегистрированных на рейс.
// для групповой печати посадочных талонов (по сути это главная процедура)
// 
// Параметры:
//  ШаблонБилета - ТабличныйДокумент
//  Параметры - Структура
Процедура ПолучитьВсехЗарегистрированныхНаРейс(ШаблонБилета, Параметры) Экспорт
	
	ШаблонБилета.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = ПолучитьТекстЗапросаВыборкиРегистрации();	
		
	Запрос.УстановитьПараметр("Рейс", Параметры.Рейс);
			 
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда 
		Сообщение = СтрШаблон("На рейсе %1 пока нет забронированных мест", Параметры.Рейс);
		чартеры_ОбщиеПроцедурыИФункцииСервер.СообщитьПользователю(Сообщение);
		Возврат;
	Иначе	
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			Макет = ПолучитьОбщийМакет(чартеры_ОбщиеПроцедурыИФункцииСервер.ПолучитьШаблонПосадочногоТалона(Выборка.КлассМеста));
			ОбластьДанных = Макет.ПолучитьОбласть("Билет");
			ЗаполнитьЗначенияСвойств(ОбластьДанных.Параметры, Выборка);
			ШаблонБилета.Вывести(ОбластьДанных);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры	

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьТекстЗапросаВыборкиРегистрации()
	
	ТестЗапроса = "ВЫБРАТЬ
	|	чартер_ЗаписьНаРейс.Организация КАК Организация,
	|	чартер_ЗаписьНаРейс.ФИО КАК ФИО,
	|	чартер_ЗаписьНаРейс.ФИОАнглийское КАК ФИОАнглийское,
	|	чартер_ЗаписьНаРейс.Место КАК Место,
	|	чартер_ЗаписьНаРейс.Рейс КАК Рейс,
	|	чартер_ЗаписьНаРейс.ДополнительныйБагаж КАК ДополнительныйБагаж,
	|	чартер_ЗаписьНаРейс.Место.Класс КАК КлассМеста
	|ПОМЕСТИТЬ втРегистрацияСотрудника
	|ИЗ
	|	РегистрСведений.чартер_ЗаписьНаРейс КАК чартер_ЗаписьНаРейс
	|ГДЕ
	|	чартер_ЗаписьНаРейс.Рейс = &Рейс
	|	И чартер_ЗаписьНаРейс.СтатусНаРейсе = ЗНАЧЕНИЕ(Перечисление.чартер_СтатусыНаРейсе.ЗаписанНаРейс)
	|	И НЕ чартер_ЗаписьНаРейс.Место = ЗНАЧЕНИЕ(Справочник.чартер_ПосадочныеМестаНаРейс.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	чартер_Рейсы.Наименование КАК Наименование,
	|	чартер_Рейсы.ДатаРейса КАК ДатаРейса,
	|	чартер_Рейсы.МаршрутПерелета КАК МаршрутПерелета,
	|	СТРОКА(чартер_Рейсы.Багаж) + "" кг"" КАК Багаж,
	|	чартер_Рейсы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ втРейс
	|ИЗ
	|	Справочник.чартер_Рейсы КАК чартер_Рейсы
	|ГДЕ
	|	чартер_Рейсы.Ссылка = &Рейс
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втРегистацияСотрудника.Организация.Представление КАК Организация,
	|	втРегистацияСотрудника.ФИО КАК ФИО,
	|	втРегистацияСотрудника.ФИОАнглийское КАК ФИОАнглийское,
	|	втРейс.ДатаРейса КАК ДатаРейса,
	|	втРейс.МаршрутПерелета.Представление КАК Маршрут,
	|	ВЫБОР
	|		КОГДА втРегистацияСотрудника.ДополнительныйБагаж
	|			ТОГДА втРейс.Багаж + ""х 2""
	|		ИНАЧЕ втРейс.Багаж
	|	КОНЕЦ КАК Багаж,
	|	втРегистацияСотрудника.Место.Представление КАК Место,
	|	втРейс.Наименование КАК НомерРейса,
	|	втРегистацияСотрудника.ДополнительныйБагаж КАК ДополнительныйБагаж,
	|	втРегистацияСотрудника.КлассМеста КАК КлассМеста
	|ИЗ
	|	втРегистрацияСотрудника КАК втРегистацияСотрудника
	|		ПОЛНОЕ СОЕДИНЕНИЕ втРейс КАК втРейс
	|		ПО втРегистацияСотрудника.Рейс = втРейс.Ссылка
	|ГДЕ
	|	НЕ втРегистацияСотрудника.Место = НЕОПРЕДЕЛЕНО";
	
	Возврат ТестЗапроса;
	
КонецФункции

#КонецОбласти
	
#КонецЕсли	