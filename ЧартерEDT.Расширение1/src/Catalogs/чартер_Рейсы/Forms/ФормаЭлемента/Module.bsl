#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьВидимость();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
    Оповестить("ИзмененаИнструкция", Объект.Ссылка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыКвотирование

&НаКлиенте
Процедура КвотированиеОрганизацияПриИзменении(Элемент)
	
	Если Объект.Ссылка.Пустая() Тогда
		чартер_ОбщиеПроцедурыИфункцииКлиент.СообщитьПользователю("Необходимо записать документ!");
		Возврат;
	КонецЕсли;	
	
    Если ЗначениеЗаполнено(Элементы.Квотирование.ТекущиеДанные.Организация) 
		И ПроверитьДубльОрганизацииНасервере(Объект.Ссылка, Элементы.Квотирование.ТекущиеДанные.Организация) Тогда	
		
		Элементы.Квотирование.ТекущиеДанные.Организация = Неопределено;
		Элементы.Квотирование.ТекущиеДанные.КвотаНаРейс = 0;
		
	КонецЕсли;   
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗагрузитьПаспорт(Команда)
	
    ВыборФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ВыборФайла.МножественныйВыбор = Ложь;
	ВыборФайла.Заголовок = НСтр("ru = 'Выбор файла'");
	ВыборФайла.Фильтр = НСтр("ru = 'Все файлы (*.*)|*.*'");
	
	РабочийКаталог = "";
	ВыборФайла.Каталог = РабочийКаталог;	
	Результат = ВыборФайла.Выбрать();
	ЗагрузитьПаспортВыборФайла(ВыборФайла.ВыбранныеФайлы, Новый Структура("ВыборФайла", ВыборФайла));	
    
КонецПроцедуры

&НаКлиенте
Процедура ПосмотретьПаспорт(Команда)
	
    ДвДанные = ПолучитьИнструкцию();
	ДанныеФайла = Новый Структура("ДвДанные, ИмяФайла", ДвДанные, Объект.Наименование + "." + Объект.Расширение);
		
	ОткрытьИнструкцию(ДанныеФайла);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокСформирован(Команда)
	
	СписокСформированНаСервере(); 
	ОбновитьВидимость();    
	
КонецПроцедуры 

&НаКлиенте
Процедура ЗаполнитьКвоты(Команда)
	
	ОбработчикВопроса = Новый ОписаниеОповещения("ПродолжитьВыполнениеПослеОтветаНаВопросЗаполнитьКвоты", ЭтотОбъект);		
	ПоказатьВопрос(ОбработчикВопроса, "Заполнить квоты на рейс по умолчанию для этого периода?", РежимДиалогаВопрос.ДаНет); 
    
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПрикрепленныеФайлы(Команда)
	
	Если Объект.Ссылка.Пустая() Тогда
		ПоказатьПредупреждение(, "Перед добавлением файлов необходимо записать объект.");
		Возврат;
	КонецЕсли;
	
	ОткрытьФорму("ОбщаяФорма.чартер_ПрикрепленныеФайлы", Новый Структура("ВладелецФайлов", Объект.Ссылка));
	
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПродолжитьВыполнениеПОслеОтветаНаВопросЗаполнитьКвоты(Результат, Параметры) Экспорт

    Если Результат = КодВозвратаДиалога.Нет Тогда
        Возврат;
	КонецЕсли;
	
	ЗаполнитьКвотыНаСервере();	
		
КонецПроцедуры

&НаСервере
Procedure ЗаполнитьКвотыНаСервере()

	ОбъектКвотирование = РеквизитФормыВЗначение("Объект");
	ОбъектКвотирование.ЗаполнитьКвотированиеРейса();
	ЗначениеВРеквизитФормы(ОбъектКвотирование, "Объект");

EndProcedure

&НаСервере
Процедура СписокСформированНаСервере()
	
	Рассылка = чартеры_ОбщиеПроцедурыИФункцииСервер.ПолучитьРассылкуПоСобытию(
																	Перечисления.чартер_СобытияРасссылок.БортСформирован);
	ПараметрыРассылки = Новый Структура;
	ПараметрыРассылки.Вставить("Борт", Объект.Наименование);	
	
	чартеры_ОбщиеПроцедурыИФункцииСервер.ОтправитьРассылки(Рассылка, ПараметрыРассылки);
		
	Объект.БортСформирован = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьВидимость()

	Если Объект.Архив Тогда
		
		ТолькоПросмотр = Истина;
		
	КонецЕсли;
	
	Если Объект.ПаспортРейсаЗагружен Тогда
		Элементы.ЗагрузитьПаспорт.Заголовок = "Обновить паспорт";
		Элементы.ПосмотретьПаспорт.Видимость = Истина;
	КонецЕсли;	
	
	Если НЕ Объект.ТипВС = Неопределено Тогда
		Элементы.ТипВС.ТолькоПросмотр = ЕстьБроньНаРейс();		
	КонецЕсли;
	              	                                     
КонецПроцедуры  

&НаСервере
Функция ЕстьБроньНаРейс()
	
	Возврат ЗначениеЗаполнено(чартеры_ОбщиеПроцедурыИФункцииСервер.ЭтоЗабронированныеМеста(Объект.Ссылка));
	
КонецФункции

&НаКлиенте
Процедура ЗагрузитьПаспортВыборФайла(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	ВыборФайла = ДополнительныеПараметры.ВыборФайла;
	Результат = (ВыбранныеФайлы <> Неопределено);
	
	Если Результат Тогда
		ИмяФайла = ВыборФайла.ПолноеИмяФайла;
		МассивСтр = чартеры_ОбщиеПроцедурыИФункцииСервер.РазложитьСтрокуПоТочкамИСлэшам(ИмяФайла);
		Объект.Расширение = МассивСтр[МассивСтр.Количество() - 1];  
		
		Если Объект.Наименование = "" Тогда
			ИмяБезКаталога = Прав(ВыборФайла.ПолноеИмяФайла
												, (СтрДлина(ВыборФайла.ПолноеИмяФайла) - СтрДлина(ВыборФайла.Каталог)));
			ИмяБезРасширения = Лев(ИмяБезКаталога, СтрДлина(ИмяБезКаталога) - 4);
			Объект.Наименование = ИмяБезРасширения;		
		КонецЕсли;	
		
		АдресВХ = "";
		//@skip-check undefined-function-or-procedure
		ПоместитьФайл(АдресВХ, ИмяФайла, , Ложь);
		ЗагрузитьПаспортНаСервере(АдресВХ);
		Объект.ПаспортРейсаЗагружен = Истина;
		ОбновитьВидимость();
		Объект.ДатаОбновления = ТекущаяДата();				
	КонецЕсли;  	

КонецПроцедуры 

&НаСервере
Процедура ЗагрузитьПаспортНаСервере(АдресВХ)

	ДвДанные = ПолучитьИзВременногоХранилища(АдресВХ);
	
	ОбъектЗапись = РеквизитФормыВЗначение("Объект");
	ОбъектЗапись.ДвДанные = Новый ХранилищеЗначения(ДвДанные);
	ОбъектЗапись.Записать();
	ЗначениеВРеквизитФормы(ОбъектЗапись, "Объект");

КонецПроцедуры 

&НаСервере
Функция ПолучитьИнструкцию()

	Паспорт = Объект.Ссылка.ПолучитьОбъект();
	Файл = Паспорт.ДвДанные.Получить();
	
	Возврат Файл;
	
КонецФункции

 &НаКлиенте
Процедура ОткрытьИнструкцию(ДвДанныеПаспорта)

	Если Не ДвДанныеПаспорта = Неопределено Тогда 
		ИмяФайла = ДвДанныеПаспорта.ИмяФайла;
		Если ИмяФайла = "" Тогда
			ИмяФайла = "ПаспортРейса.pdf";
		КонецЕсли;
		#Если ВебКлиент ИЛИ ТонкийКлиент Тогда
			АдресФайла = ПоместитьВоВременноеХранилище(ДвДанныеПаспорта.ДвДанные);
			ПолучитьФайл(АдресФайла, ИмяФайла);
		#Иначе
			ДвДанныеПаспорта.ДвДанные.Записать(КаталогВременныхФайлов() + "\" + ИмяФайла);
			ЗапуститьПриложение(КаталогВременныхФайлов() + "\" + ИмяФайла);
		#КонецЕсли
	КонецЕсли;
	
КонецПроцедуры
   
&НаСервереБезКонтекста
Функция ПроверитьДубльОрганизацииНаСервере(Документ, Организация)
	
	 ЭтоДубль = Ложь;
	
	 Запрос = Новый Запрос;
	 Запрос.Текст = "ВЫБРАТЬ
	                |	чартер_РейсыКвотирование.Организация.Представление КАК ОрганизацияПредставление
	                |ИЗ
	                |	Справочник.чартер_Рейсы.Квотирование КАК чартер_РейсыКвотирование
	                |ГДЕ
	                |	чартер_РейсыКвотирование.Организация = &ПроверяемаяОрганизация
	                |	И чартер_РейсыКвотирование.Ссылка = &Ссылка";
	 
	 Запрос.УстановитьПараметр("Ссылка", Документ);
	 Запрос.УстановитьПараметр("ПроверяемаяОрганизация", Организация);
	 
	 Результат = Запрос.Выполнить();	 	 
	 Если НЕ Результат.Пустой() Тогда
	 	чартеры_ОбщиеПроцедурыИФункцииСервер.СообщитьПользователю("Данная организация уже есть в списке квотирования!");				
		ЭтоДубль = Истина;
	 КонецЕсли;
	 
	 Возврат ЭтоДубль;
		                            	 
КонецФункции

#КонецОбласти