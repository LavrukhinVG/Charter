#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
    Если Объект.ПометкаУдаления Тогда
		ЭтотОбъект.ТолькоПросмотр = Истина;
	КонецЕсли;  
	
	Если Объект.Ссылка.Пустая() Тогда		
		Объект.Автор = SessionParameters.CurrentUser;
		Объект.СобытияРассылок = Перечисления.чартер_СобытияРасссылок.БезСобытия;
	КонецЕсли;	
	
	// Вложения.
	Если СтруктураВложенийПисьмаВФорматеHTML = Неопределено Тогда
		СтруктураВложенийПисьмаВФорматеHTML = Новый Структура;
	КонецЕсли;
                         
КонецПроцедуры     

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// Чтение хранилища значений
	Если ТекущийОбъект.ПисьмоВФорматеHTML Тогда
		СтруктураВложенийПисьмаВФорматеHTML = ТекущийОбъект.КартинкиПисьмаВФорматеHTML.Получить();
		Если СтруктураВложенийПисьмаВФорматеHTML = Неопределено Тогда
			СтруктураВложенийПисьмаВФорматеHTML = Новый Структура;
		КонецЕсли;
		ТекстПисьмаФорматированныйДокумент.УстановитьHTML(ТекущийОбъект.ТекстПисьмаВФорматеHTML, СтруктураВложенийПисьмаВФорматеHTML);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)

	НепроверяемыеРеквизиты = Новый Массив();
	
	Если Объект.РассылкаАктивна И Не ЗначениеЗаполнено(Объект.Адресаты) Тогда
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru = 'Перед включением рассылки необходимо заполнить адресатов!'");
		Сообщение.Поле = "Объект.Адресаты"; 
		Сообщение.УстановитьДанные(ЭтотОбъект); 
		Сообщение.Сообщить();
		Отказ = Истина; 
		НепроверяемыеРеквизиты.Добавить("Объект.Адресаты");
		
	КонецЕсли;

	чартеры_ОбщиеПроцедурыИФункцииСервер.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, НепроверяемыеРеквизиты);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОтправитьРассылку(Команда)
	ОтправитьРассылкуНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьТипТекстаНаHTML(Команда)

	Модифицированность = Истина;
	Объект.ПисьмоВФорматеHTML = Истина;
	ТекстПисьмаИзHTML = СокрЛП(ТекстПисьмаФорматированныйДокумент.ПолучитьТекст());
	Если ТекстПисьмаИзHTML <> Объект.ТекстПисьма Тогда
		ТекстПисьмаФорматированныйДокумент.Удалить();
		ТекстПисьмаФорматированныйДокумент.Добавить(Объект.ТекстПисьма, ТипЭлементаФорматированногоДокумента.Текст);
	КонецЕсли;
	ТекущийЭлемент = Элементы.ТекстПисьмаФорматированныйДокумент;	

КонецПроцедуры

&НаКлиенте
Процедура ИзменитьТипТекстаНаОбычный(Команда)

	Модифицированность = Истина;
	Объект.ПисьмоВФорматеHTML = Ложь;
	ТекстПисьмаИзHTML = СокрЛП(ТекстПисьмаФорматированныйДокумент.ПолучитьТекст());
	Если Объект.ТекстПисьма <> ТекстПисьмаИзHTML Тогда
		Объект.ТекстПисьма = ТекстПисьмаИзHTML;
	КонецЕсли;
	ТекущийЭлемент = Элементы.ТекстПисьма;	

КонецПроцедуры  

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ТекущийОбъект.КартинкиПисьмаВФорматеHTML = Неопределено;
	Если ТекущийОбъект.ПисьмоВФорматеHTML Тогда
		ТекущийОбъект.ТекстПисьма = СокрЛП(ТекстПисьмаФорматированныйДокумент.ПолучитьТекст());
		Если ТекущийОбъект.ТекстПисьма = "" Тогда
			ТекущийОбъект.ТекстПисьмаВФорматеHTML = "";
		Иначе
			ТекстПисьмаФорматированныйДокумент.ПолучитьHTML(ТекущийОбъект.ТекстПисьмаВФорматеHTML, СтруктураВложенийПисьмаВФорматеHTML);
			Если ТипЗнч(СтруктураВложенийПисьмаВФорматеHTML) = Тип("Структура")
				И СтруктураВложенийПисьмаВФорматеHTML.Количество() > 0 Тогда
				ТекущийОбъект.КартинкиПисьмаВФорматеHTML = Новый ХранилищеЗначения(СтруктураВложенийПисьмаВФорматеHTML, Новый СжатиеДанных(9));
			КонецЕсли;
			ТекущийОбъект.ТекстПисьма = ТекстПисьмаФорматированныйДокумент.ПолучитьТекст();
		КонецЕсли;
	КонецЕсли;
                
КонецПроцедуры

#КонецОбласти    

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОтправитьРассылкуНаСервере()
	
	Если Модифицированность Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Сохраните изменения перед отправкой рассылки!"; 		
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;	
	
	Если НЕ Объект.РассылкаАктивна Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Выбранная рассылка не активна!"; 
		Сообщение.Поле = "Объект.РассылкаАктивна";
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
		
	чартеры_ОбщиеПроцедурыИФункцииСервер.ОтправитьРассылки(Объект.Ссылка);		
	                                      
КонецПроцедуры


#КонецОбласти    